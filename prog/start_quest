#!/opt/local/bin/tcsh
#
# start_quest
#
# This shell starts the quest telescope control program calle questctl
#
# It first waits for the sun to go down.
# Then it launches questctl with the "-d" option, which makes questctl
# wait for the dome to open before serving commands. If the sun comes up
# before the dome opens, questctl exits without serving commands. Otherwise,
# it serves commands until the sun comes up and then exits.
#
cd /home/neat/yale/bin
set d = `date -u +"%Y%m%d.%H%M%S"`
set STARTUP_LOG = "startup.log.$d"
#
date -u >! $STARTUP_LOG
./update_filter_log >>& $STARTUP_LOG
#
# wait for sun to go down
#
echo "waiting for sun to go down" >> $STARTUP_LOG
#
set s = `sunup | grep -v "#"`
while ( $s != 0 )
  date -u >> $STARTUP_LOG
  if ( $s == 1 ) then
    echo "sun is still up" >> $STARTUP_LOG
  else if ( $s == -1 ) then
    echo "error getting sun position" >> >> $STARTUP_LOG
    exit
  endif
  sleep 120
  set s = `sunup | grep -v "#"`
end
date -u >> $STARTUP_LOG
echo "sun is down" >> $STARTUP_LOG
#
# launch questctl with -d option
#
echo "launching questctl -d" >> $STARTUP_LOG
#
set d = `date -u +"%Y%m%d.%H%M%S"`
#
echo "questctl -d >& questctl.log.$d &" >> $STARTUP_LOG
questctl -d >& questctl.log.$d &
#
